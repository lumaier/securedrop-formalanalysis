theory SecureDrop
begin

builtins: symmetric-encryption, diffie-hellman, signing

rule Register_pk:
  [ Fr(~ltk) ]
  -->
  [ !Ltk($A, ~ltk), !Pk($A, pk(~ltk)), Out(pk(~ltk))]

rule Reveal_ltk:
  [ !Ltk(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_dh_pk:
  let public_component = 'g'^~ltk in
  [ Fr(~ltk) ]
  -->
  [ !Ltk_dh($A, ~ltk), !Pk_dh($A, public_component), Out(public_component)]

rule Reveal_dh_ltk:
  [ !Ltk_dh(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Init:
  let je_pk = 'g'^~je_sk
      sig_je_pk = sign(je_pk, j_sk)
  in
  [ !Ltk($J, j_sk)
  , Fr(~je_sk) ]
  --[ ReachJournalistInit($J) ]->
  [ Out(<je_pk, sig_je_pk>)
  , Journalist_Ephemeral_Key($J, ~je_sk) ]

rule Journalist_Enrollment:
  let sig_j_pk = sign(j_pk, nr_sk)
  in
  [ !Ltk($NR, nr_sk)
  , !Pk($J, j_pk) ]
  --[ Journalist_Verified($NR, $J) ]->
  [ Out(sig_j_pk) ]

rule Source_Submits:
  let k = je_pk^~me_sk
      c = senc(<~m, s_pk>, k)
      me_pk = 'g'^~me_sk
  in
  [ !Pk($NR, nr_pk)
  , !Pk($J, j_pk)
  , !Pk_dh($S, s_pk)
  , In(<je_pk, sig_je_pk, j_pk, sig_j_pk>)
  , Fr(~m)
  , Fr(~me_sk) ]
  --[ Eq(verify(sig_j_pk, j_pk, nr_pk), true)
    , Eq(verify(sig_je_pk, je_pk, j_pk), true)
    , Source_Submission($S, $NR, ~m)
    ]->
  [ Out(<c, me_pk>)
  , Source_Submit($S, $J) ]

rule Journalist_Recv:
  let k = me_pk^je_sk 
      msg = sdec(c, k)
      m = fst(msg)
      s_pk = snd(msg)
  in
  [ In(<c,me_pk>)
  , Journalist_Ephemeral_Key($J, je_sk) ]
  --[ Journalist_Read($J, m) ]->
  [ Journalist_Recv($J, m, s_pk) ]

rule Journalist_Reply:
  let k = s_pk^~me_sk
      je_pk = 'g'^~je_sk
      c = senc(<~m, je_pk>, k)
      me_pk = 'g'^~me_sk
  in
  [ Journalist_Recv($J, m_old, s_pk)
  , Fr(~m)
  , Fr(~me_sk)
  , Fr(~je_sk) ]
  --[ Journalist_Response($J, m_old, ~m)
    ]->
  [ Out(<c, me_pk>) ]

rule Source_Recv:
  let k = me_pk^s_sk
      msg = sdec(c, k)
      m = fst(msg)
      je_pk = snd(msg)
  in
  [ In(<c, me_pk>)
  , Source_Submit($S, $J)
  , !Ltk_dh($S, s_sk) ]
  --[ Source_Read($S, m) ]->
  []

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2" 

lemma Executability_Source_Submission:
  exists-trace
  "Ex #t1 #t2 s j nr m1. 
        Source_Submission(s, nr, m1) @ #t1
      & Journalist_Read(j, m1) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Executability_Journalist_Response:
  exists-trace
  "Ex #t1 #t2 s j m1 m2. 
        Journalist_Response(j, m1, m2) @ #t1
      & Source_Read(s, m2) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Confidentiality_Source_Submit:
  all-traces
  "All #t1 s nr m. Source_Submission(s, nr, m) @ #t1
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. SomeCompromise() @ #x)"

lemma Verified_Journalist:
  all-traces
  "All #t1 #t2 s nr j m. 
      Source_Submission(s, nr, m) @ #t1
    & Journalist_Read(j, m) @ #t2
    ==> (Ex #x. Journalist_Verified(nr, j) @ #x & #x < #t1) | (Ex #x. SomeCompromise() @ #x)"

lemma Confidentiality_Journalist_Response:
  all-traces
  "All #t1 #t2 s nr j m_old m. Source_Submission(s, nr, m_old) @ #t1 & Journalist_Response(j, m_old, m) @ #t2
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. SomeCompromise() @ #x)"

end