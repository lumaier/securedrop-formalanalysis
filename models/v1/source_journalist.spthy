theory SourceJournalistMessaging
begin

builtins: symmetric-encryption, diffie-hellman, signing

rule Register_journalist_pk:
  [ Fr(~ltk) ]
  -->
  [ !Ltk_journalist($A, ~ltk), !Pk_journalist($A, pk(~ltk)), Out(pk(~ltk))]

rule Reveal_journalist_ltk:
  [ !Ltk_journalist(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_source_pk:
  let public_component = 'g'^~ltk in
  [ Fr(~ltk) ]
  -->
  [ !Ltk_source($A, ~ltk), !Pk_source($A, public_component), Out(public_component)]

rule Reveal_source_ltk:
  [ !Ltk_source(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Init:
  let je_pk = 'g'^~je_sk
      sig_je_pk = sign(je_pk, j_sk)
  in
  [ !Ltk_journalist($J, j_sk)
  , Fr(~je_sk)
  ]
  --[ ReachJournalistInit($J) ]->
  [ Out(<je_pk, sig_je_pk>)
  , Journalist_Ephemeral_Key($J, ~je_sk)
  ]

rule Source_Submit:
  let k = je_pk^~me_sk
      c = senc(<~m, s_pk>, k)
      me_pk = 'g'^~me_sk
  in
  [ !Pk_journalist($J, j_pk)
  , !Pk_source($S, s_pk)
  , In(<je_pk, sig_je_pk>)
  , Fr(~m)
  , Fr(~me_sk) ]
  --[ Eq(verify(sig_je_pk, je_pk, j_pk), true)
    , Source_Submission($S, $J, ~m)
    ]->
  [ Out(<c,me_pk>) ]

rule Journalist_Recv:
  let k = me_pk^je_sk 
      msg = sdec(c, k)
      m = fst(msg)
  in
  [ In(<c,me_pk>)
  , Journalist_Ephemeral_Key($J, je_sk) ]
  --[ Journalist_Read($J, m) ]->
  []

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

lemma Executability:
  exists-trace
  "Ex #t1 #t2 #t3 s j m. ReachJournalistInit(j) @ #t1 & Source_Submission(s, j, m) @ #t2 & Journalist_Read(j, m) @ #t3 
    & (not Ex #x. SomeCompromise() @ #x)"

lemma Confidentiality:
  all-traces
  "All #t1 s j m. Source_Submission(s, j, m) @ #t1
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. Reveal(s) @ #x) | (Ex #x. Reveal(j) @ #x)"

end