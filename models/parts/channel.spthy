rule ClientOut[color=#FFFFFF]:
  [ Client_Out(~sess, $Server, msg), !SecureDropInstance($Server) ]
  -->
  [ !Client(~sess, $Server, msg) ]

rule ServerOut[color=#FFFFFF]:
  [ Server_Out(~sess, $Server, msg), !SecureDropInstance($Server) ]
  -->
  [ !Server(~sess, $Server, msg) ]

rule ClientToServer[color=#FFFFFF]:
  [ !Client(~sess, $Server, msg) ]
  -->
  [ Server_In(~sess, $Server, msg) ]

rule ServerToClient[color=#FFFFFF]:
  [ !Server(~sess, $Server, msg) ]
  -->
  [ Client_In(~sess, $Server, msg) ]

rule AdversaryClientOut[color=#FF6961]:
  [ In(~sess), In(msg), !SecureDropInstance($Server) ]
  -->
  [ !Client(~sess, $Server, msg) ]

rule ServerCompromise:
  [ !SecureDropInstance($Server) ]
  --[ AdversaryActivity()
    , SecureDropInstanceCompromise($Server) ]->
  [ !ActiveCompromise($Server) ]

rule Snapshot:
  [ !SecureDropInstance($Server) ]
  --[ AdversaryActivity()
    , SecureDropInstanceReadAccess($Server) ]->
  [ !PassiveCompromise($Server) ]

rule ServerToAdversaryClient[color=#FF6961]:
  [ In(adversarySess), !Server(adversarySess, $Server, msg) ]
  --[ AdversaryActivity()
    , ServerSources(msg) ]->
  [ Out(msg) ]

rule ServerAdversaryFromClient[color=#FF6961]:
  [ !Client(~sess, $Server, msg), !ActiveCompromise($Server) ]
  --[ AdversaryActivity()
    , ClientSources(msg) ]->
  [ Out(msg) ]

rule ServerAdversaryToClient[color=#FF6961]:
  [ !Client(~sess, $Server, msg), !ActiveCompromise($Server), In(m) ]
  --[ AdversaryActivity() ]->
  [ !Server(~sess, $Server, m) ]
