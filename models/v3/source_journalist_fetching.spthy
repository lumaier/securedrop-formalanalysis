theory SourceJournalistMessaging
begin

builtins: symmetric-encryption, diffie-hellman, signing

rule Register_signing_pk:
  [ Fr(~ltk) ]
  -->
  [ !Ltk_signing($A, ~ltk), !Pk_signing($A, pk(~ltk)), Out(pk(~ltk))]

rule Reveal_signing_ltk:
  [ !Ltk_signing(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_dh_pk:
  let public_component = 'g'^~ltk in
  [ Fr(~ltk) ]
  -->
  [ !Ltk_dh($A, ~ltk), !Pk_dh($A, public_component), Out(public_component)]

rule Reveal_dh_ltk:
  [ !Ltk_dh(A, ltk) ]
  --[ Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Init:
  let je_pk = 'g'^~je_sk
      sig_je_pk = sign(je_pk, j_sk)
      sig_jc_pk = sign(jc_pk, j_sk)
  in
  [ !Ltk_signing($J, j_sk)
    , !Pk_dh($J, jc_pk)
    , Fr(~je_sk)
  ]
  --[ ReachJournalistInit($J) ]->
  [ Out(<je_pk, sig_je_pk, jc_pk, sig_jc_pk>)
  , Journalist_Ephemeral_Key($J, ~je_sk)
  ]

rule Source_Submit:
  let k = je_pk^~me_sk
      c = senc(<~m, s_pk>, k)
      me_pk = 'g'^~me_sk
      m_gdh = jc_pk^~me_sk
  in
  [ !Pk_signing($J, j_pk)
  , !Pk_dh($S, s_pk)
  , In(<je_pk, sig_je_pk, jc_pk, sig_jc_pk>)
  , Fr(~m)
  , Fr(~me_sk) ]
  --[ Eq(verify(sig_je_pk, je_pk, j_pk), true)
    , Eq(verify(sig_jc_pk, jc_pk, j_pk), true)
    , Source_Submission($S, $J, ~m)
    ]->
  [ Out(<c,m_gdh, me_pk>)
  , Source_Submit($S) ]

rule Journalist_Fetch:
  let kmid = pmgdh^jc_sk 
      mid = sdec(enc_m_id, kmid)
  in
  [ In(<pmgdh, enc_m_id>)
  , !Ltk_dh($J, jc_sk)]
  --[ Journalist_Fetched($J, mid) ]->
  [ Out(mid) ]

rule Journalist_Recv:
  let k = me_pk^je_sk 
      msg = sdec(c, k)
      m = fst(msg)
      s_pk = snd(msg)
  in
  [ In(<c, me_pk>)
  , Journalist_Ephemeral_Key($J, je_sk) ]
  --[ Journalist_Read($J, m) ]->
  [ Journalist_Recv($J, m, s_pk) ]

rule Journalist_Reply:
  let k = s_pk^~me_sk
      je_pk = 'g'^~je_sk
      c = senc(<~m, je_pk>, k)
      me_pk = 'g'^~me_sk
  in
  [ Journalist_Recv($J, m_old, s_pk)
  , Fr(~m)
  , Fr(~me_sk)
  , Fr(~je_sk) ]
  --[ Journalist_Response($J, m_old, ~m)
    ]->
  [ Out(<c, me_pk>) ]

rule Source_Recv:
  let k = me_pk^s_sk
      msg = sdec(c, k)
      m = fst(msg)
      je_pk = snd(msg)
  in
  [ In(<c, me_pk>)
  , Source_Submit($S)
  , !Ltk_dh($S, s_sk) ]
  --[ Source_Read($S, m) ]->
  []

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2" 

lemma Executability:
  exists-trace
  "Ex #t1 #t2 #t3 #t4 #t5 #t6 s j m1 m2 mid. 
        ReachJournalistInit(j) @ #t1 
      & Source_Submission(s, j, m1) @ #t2 
      & Journalist_Fetched(j, mid) @ #t3
      & Journalist_Read(j, m1) @ #t4 
      & Journalist_Response(j, m1, m2) @ #t5
      & Source_Read(s, m2) @ #t6
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Confidentiality_Source_Submit:
  all-traces
  "All #t1 s j m. Source_Submission(s, j, m) @ #t1
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. Reveal(s) @ #x) | (Ex #x. Reveal(j) @ #x)"

lemma Confidentiality_Journalist_Response:
  all-traces
  "All #t1 #t2 s j m_old m. Source_Submission(s, j, m_old) @ #t1 & Journalist_Response(j, m_old, m) @ #t2
    ==> (not Ex #x. K(m) @ #x) | (Ex #x. Reveal(s) @ #x) | (Ex #x. Reveal(j) @ #x)"

end