theory SecureDrop
begin

builtins: symmetric-encryption, asymmetric-encryption, diffie-hellman, signing

// KDF
functions: kdf/1

// KEM
functions: pqpk/1, encap/2, decap/2
equations: decap(encap(k, pqpk(sk)), sk) = k

/*

Color semantics:

- Yellow (#FDFD96)      Key setup/distribution
- Red (#FF6961)         Adversary activity
- Light blue (#B4D9EF)  Message Fetching
- Dark blue (#6495ED)   Messaging
- Orange (#FF7F50)      Registration/Enrollment

*/

// PQ Attacker
rule PQAttackerStart[color=#FF6961]:
  [] --[ PQAttack(), SomeCompromise() ]-> [ !PQAttacker() ]

rule PQAttacker[color=#FF6961]:
  [ !PQAttacker(), !NonPQSecKey(k) ] --> [ Out(k) ]

restriction PQAttacker:
  "All #compr #t. PQAttack() @ #compr & PrePQAttack() @ #t ==> #t < #compr"

// Key Registration

rule Register_Newsroom_Key [color=#FDFD96]:
  let public_key = pk(~ltk) in
  [ Fr(~ltk) ]
  --[ PrePQAttack() ]->
  [ !Ltk_Newsroom($A, ~ltk)
  , !Pk_Newsroom($A, public_key)
  , Out(public_key)
  , !NonPQSecKey(~ltk) ]

rule Reveal_Newsroom_Key [color=#FF6961]:
  [ !Ltk_Newsroom(A, ~ltk) ]
  --[ Reveal_Newsroom_Key(A)
    , SomeCompromise() ]->
  [ Out(~ltk) ]

rule Register_Source_Passphrase [color=#FDFD96]:
  let s_dh_sk = kdf(<'DH', ~passphrase>)
      s_pke_sk = kdf(<'PKE', ~passphrase>)
      s_kem_sk = kdf(<'KEM', ~passphrase>)
      s_dh_pk = 'g'^s_dh_sk
      s_pke_pk = pk(s_pke_sk)
      s_kem_pk = pqpk(s_kem_sk)
  in
  [ Fr(~passphrase) ]
  --[ Register_Source(s_dh_pk)
    , PrePQAttack() ]->
  [ !Ltk_Source_Passphrase($A, ~passphrase)
  , Ltk_Source_DH_Key_Reveal($A, s_dh_sk)
  , Ltk_Source_PKE_Key_Reveal($A, s_pke_sk)
  , Ltk_Source_KEM_Key_Reveal($A, s_kem_sk)
  , !NonPQSecKey(s_dh_sk)
  , !NonPQSecKey(s_pke_sk)
  , Out(<s_dh_pk, s_pke_pk, s_kem_pk>) ]

// TODO: reveal passphrase

rule Reveal_Source_DH_Key [color=#FF6961]:
  let s_dh_pk = 'g'^s_dh_sk
  in
  [ Ltk_Source_DH_Key_Reveal($A, s_dh_sk) ]
  --[ Reveal_Source_DH_Key($A)
    , Reveal_Source_DH_Key(s_dh_pk)
    , SomeCompromise() ]->
  [ Out(s_dh_sk) ]


rule Reveal_Source_PKE_Key [color=#FF6961]:
  let s_pke_pk = pk(s_pke_sk)
  in
  [ Ltk_Source_PKE_Key_Reveal($A, s_pke_sk) ]
  --[ Reveal_Source_PKE_Key($A)
    , Reveal_Source_PKE_Key(s_pke_pk)
    , SomeCompromise() ]->
  [ Out(s_pke_sk) ]

rule Reveal_Source_KEM_Key [color=#FF6961]:
  let s_kem_pk = pk(s_kem_sk)
  in
  [ Ltk_Source_KEM_Key_Reveal($A, s_kem_sk) ]
  --[ Reveal_Source_KEM_Key($A)
    , Reveal_Source_KEM_Key(s_kem_pk)
    , SomeCompromise() ]->
  [ Out(s_kem_sk) ]

// Journalist Enrollment

rule Journalist_Enrollment [color=#FF7F50]:
  let j_sig_pk = pk(~j_sig_sk)
      j_dh_pk = 'g'^~j_dh_sk
      sig_j_pk = sign(<'signature', <j_sig_pk, j_dh_pk>>, ~nr_sk)
  in
  [ !Ltk_Newsroom($NR, ~nr_sk)
  , Fr(~j_sig_sk)
  , Fr(~j_dh_sk) ]
  --[ Journalist_Verified($NR, $J)
    , OnlyOnce($J)
    , PrePQAttack() ]->
  [ Out(<<j_sig_pk, j_dh_pk>, sig_j_pk>)
  , !Journalist_Enrolled($NR, $J)
  , !Ltk_Journalist_SIG_Key($J, ~j_sig_sk)
  , !Ltk_Journalist_DH_Key($J, ~j_dh_sk)
  , !NonPQSecKey(~j_sig_sk)
  , !NonPQSecKey(~j_dh_sk) ]

rule Reveal_Journalist_SIG_Key [color=#FF6961]:
  let j_sig_pk = pk(~j_sig_sk)
  in
  [ !Ltk_Journalist_SIG_Key($A, ~j_sig_sk) ]
  --[ Reveal_Journalist_SIG_Key($A)
    , Reveal_Journalist_SIG_Key(j_sig_pk)
    , SomeCompromise() ]->
  [ Out(~j_sig_sk) ]

rule Reveal_Journalist_DH_Key [color=#FF6961]:
  let j_dh_pk = 'g'^~j_dh_sk
  in
  [ !Ltk_Journalist_DH_Key($A, ~j_dh_sk) ]
  --[ Reveal_Journalist_DH_Key($A)
    , Reveal_Journalist_DH_Key(j_dh_pk)
    , SomeCompromise() ]->
  [ Out(~j_dh_sk) ]

rule Journalist_Register_Ephemeral_Key [color=#FF7F50]:
  let je_dh_pk = 'g'^~je_dh_sk
      je_pke_pk = pk(~je_pke_sk)
      je_kem_pk = pqpk(~je_kem_sk)
      sig_je_pk = sign(<'ephemeral', <je_dh_pk, je_pke_pk, je_kem_pk>>, ~j_sig_sk)
  in
  [ !Ltk_Journalist_SIG_Key($J, ~j_sig_sk)
  , Fr(~je_dh_sk)
  , Fr(~je_pke_sk)
  , Fr(~je_kem_sk) ]
  --[ PrePQAttack() ]->
  [ Out(<<je_dh_pk, je_pke_pk, je_kem_pk>,  sig_je_pk>)
  , !Journalist_Ephemeral_Key($J, ~je_dh_sk, ~je_pke_sk, ~je_kem_sk)
  , Journalist_EDH_Key_Reveal($J, ~je_dh_sk)
  , Journalist_EPKE_Key_Reveal($J, ~je_pke_sk)
  , Journalist_EKEM_Key_Reveal($J, ~je_kem_sk)
  , !NonPQSecKey(~je_dh_sk)
  , !NonPQSecKey(~je_pke_sk) ]

rule Reveal_Journalist_EDH_Key [color=#FF6961]:
  let je_dh_pk = 'g'^~je_dh_sk
  in
  [ Journalist_EDH_Key_Reveal($A, ~je_dh_sk) ]
  --[ Reveal_Journalist_EDH_Key($A)
    , Reveal_Journalist_EDH_Key(je_dh_pk)
    , SomeCompromise() ]->
  [ Out(~je_dh_sk) ]

rule Reveal_Journalist_EPKE_Key [color=#FF6961]:
  let je_pke_pk = pk(~je_pke_sk)
  in
  [ Journalist_EPKE_Key_Reveal($J, ~je_pke_sk) ]
  --[ Reveal_Journalist_EPKE_Key($A)
    , Reveal_Journalist_EPKE_Key(je_pke_pk)
    , SomeCompromise() ]->
  [ Out(~je_pke_sk) ]

rule Reveal_Journalist_EKEM_Key [color=#FF6961]:
  let je_kem_pk = pqpk(~je_kem_sk)
  in
  [ Journalist_EKEM_Key_Reveal($J, ~je_kem_sk) ]
  --[ Reveal_Journalist_EKEM_Key($A)
    , Reveal_Journalist_EKEM_Key(je_kem_pk)
    , SomeCompromise() ]->
  [ Out(~je_kem_sk) ]

// Messaging and Fetching Protocol

rule Source_Send [color=#6495ED]:
      // Not sure if it's best to alway re-derive the source's secret keys.
      // Certainly not wrong, but leads to code-duplication (which in turn can
      // lead to errors). You could also memorize the secret keys in
      // !Ltk_Source_Passphrase.
  let s_dh_sk = kdf(<'DH', ~passphrase>)
      s_pke_sk = kdf(<'PKE', ~passphrase>)
      s_kem_sk = kdf(<'KEM', ~passphrase>)
      s_dh_pk = 'g'^s_dh_sk
      s_pke_pk = pk(s_pke_sk)
      s_kem_pk = pqpk(s_kem_sk)
      me_pk = 'g'^~me_sk
      k = kdf(<kdf(~ss), je_dh_pk^s_dh_sk, je_dh_pk^~me_sk>)
      m = <'msg_s2j', ~msg, s_pke_pk, s_kem_pk, j_sig_pk, $NR>
      enc_gs = aenc(<'s_pk', s_dh_pk>, je_pke_pk)
      ss_encap = encap(~ss, je_kem_pk)
      c = senc(m, k)
  in
  [ !Ltk_Source_Passphrase($S, ~passphrase)
  , !Pk_Newsroom($NR, nr_pk)
  , In(<<j_sig_pk, j_dh_pk>, sig_j_pk, <je_dh_pk, je_pke_pk, je_kem_pk>, sig_je_pk>)
  , Fr(~me_sk)
  , Fr(~msg)
  , Fr(~ss) ]
  --[ // check signatures of journalist's longterm signature and ephemeral encryption keys
      Eq(verify(sig_j_pk, <'signature', <j_sig_pk, j_dh_pk>>, nr_pk), true)
    , Eq(verify(sig_je_pk, <'ephemeral', <je_dh_pk, je_pke_pk, je_kem_pk>>, j_sig_pk), true)
      // sanity check of DH elements used
    // Sanity check below is redundant: you derive k above yourself. Did you
    // want to check a different key? (Could also be legacy.)
    , DHElement_Check(k)
      // Actions for Lemmas
    , Symmetric_Encryption_OUT(m, k)
    , Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, $NR, m)
    , PrePQAttack()
    ]->
  [ Out(<c, enc_gs, me_pk, ss_encap>)
  , !NonPQSecKey(~me_sk) ]

rule Journalist_Recv [color=#6495ED]:
  let ss = decap(ss_encap, ~je_kem_sk)
      j_sig_pk = pk(~j_sig_sk)
      k = kdf(<kdf(ss), s_dh_pk^~je_dh_sk, me_pk^~je_dh_sk>)
      m = <'msg_s2j', msg, s_pke_pk, s_kem_pk, j_sig_pk, $NR>
      je_dh_pk = 'g'^~je_dh_sk
      je_pke_pk = pk(~je_pke_sk)
      je_kem_pk = pqpk(~je_kem_sk)
  in
  [ In(<senc(m, k), aenc(<'s_pk', s_dh_pk>, pk(~je_pke_sk)), me_pk, ss_encap>)
  , !Journalist_Ephemeral_Key($J, ~je_dh_sk, ~je_pke_sk, ~je_kem_sk)
  , !Ltk_Journalist_SIG_Key($J, ~j_sig_sk)
  , !Journalist_Enrolled($NR, $J)]
  --[ DHElement_Check(k)
    // You can also skip this OnlyOnce and make !Journalist_Ephemeral_Key
    // non-persistent.
    , OnlyOnce(<$J, ~je_dh_sk, ~je_pke_sk, ~je_kem_sk>)
      // Actions for Lemmas
    , Symmetric_Encryption_IN(m, k)
    , Journalist_Received(j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, $NR, s_dh_pk, m)
    , PrePQAttack()
    ]->
  // I would not use tuples in your own action facts unless you must; can easily
  // lead to makes mistakes from wrong pattern-matching.
  [ Journalist_Recv($J, $NR, <s_dh_pk, s_pke_pk, s_kem_pk>, msg) ]

rule Journalist_Send [color=#6495ED]:
  let k = kdf(<kdf(~ss), s_dh_pk^~j_dh_sk, s_dh_pk^~me_sk>)
      m = <'msg_j2s', ~msg>
      c = senc(m, k)
      j_dh_pk = 'g'^~j_dh_sk
      enc_gj = aenc(<'j_pk', j_dh_pk>, s_pke_pk)
      me_pk = 'g'^~me_sk
      ss_encap = encap(~ss, s_kem_pk)
  in
  [ Journalist_Recv($J, $NR, <s_dh_pk, s_pke_pk, s_kem_pk>, msg_old)
  , !Ltk_Journalist_DH_Key($J, ~j_dh_sk)
  , Fr(~msg)
  , Fr(~me_sk)
  , Fr(~ss) ]
  --[ // Actions for Lemmas
      Symmetric_Encryption_OUT(m, k)
    , Journalist_Sent(j_dh_pk, s_dh_pk, m)
    , PrePQAttack()
    ]->
  [ Out(<c, enc_gj, me_pk, ss_encap>) 
  , !NonPQSecKey(~me_sk) ]

rule Source_Recv [color=#6495ED]:
  let s_dh_sk = kdf(<'DH', ~passphrase>)
      s_pke_sk = kdf(<'PKE', ~passphrase>)
      s_kem_sk = kdf(<'KEM', ~passphrase>)
      s_dh_pk = 'g'^s_dh_sk
      ss = decap(ss_encap, s_kem_sk)
      k = kdf(<kdf(ss), j_dh_pk^s_dh_sk, me_pk^s_dh_sk>)
      m = <'msg_j2s', msg>
  in
  [ In(<senc(m, k), aenc(<'j_pk', j_dh_pk>, pk(s_pke_sk)), me_pk, ss_encap>)
  , !Ltk_Source_Passphrase($S, ~passphrase) ]
  --[ DHElement_Check(me_pk)
    , DHElement_Check(j_dh_pk)
    , DHElement_Check(k)
      // Actions for Lemmas
    , Symmetric_Encryption_IN(m, k)
    , Source_Received(s_dh_pk, j_dh_pk, m)
    , PrePQAttack()
    ]->
  // Unused fact.
  [ Source_Recv($S, msg, $J) ]

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

restriction DHElement_Check:
  "All x #t. DHElement_Check(x) @ #t ==> not(x = 'g' | x = DH_neutral)"

restriction OnlyOnce:
  "All x #i #j. OnlyOnce(x)@#i & OnlyOnce(x)@#j ==> #i = #j"

/* Without this Source-Lemma, Tamarin thinks that the "Fetch" Rules act as decryption oracles.
   This Source-Lemma ensures that if a party gets a ciphertext, either the adversary encrypted the message (i.e, it knew the message)
   or the adversary got the ciphertext from an output in the protocol.
*/
lemma Auto_Symmetric_Encryption [sources]:
"All m key #t.
        Symmetric_Encryption_IN(m, key) @ #t
  ==>   ( (Ex #x. KU(m) @ #x & #x < #t)
        & (Ex #x. KU(key) @ #x & #x < #t))
      | (Ex #x. Symmetric_Encryption_OUT(m, key) @ #x & #x < #t)"

lemma Auto_Executability_Submission:
exists-trace
"Ex #t1 #t2 s_dh_pk j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr m.
        Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #t1
      & Journalist_Received(j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, s_dh_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Auto_Secrecy_Source_Message:
"All #t s_dh_pk j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr m. 
        Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #t
      & (not Ex #x. PQAttack() @ #x)
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x)
      // I would assume we can make the branch below stronger: Requires leak of
      // EDH *and* KEM key, no?
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)"

// Aha. You address this in a second lemma. I think it would be better to move
// the `Ex. PQAttack()` part to the right side of the implication.
lemma Auto_PQ_Secrecy_Source_Message:
"All #t s_dh_pk j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr m. 
        Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x)
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)
      | (Ex #x. Reveal_Journalist_EKEM_Key(je_kem_pk) @ #x)"

// The only difference to secrecy is that you add the `#x < #t` parts. I don't
// think that this deserves its own lemma. I think you can just use this lemma
// for secrecy (the stronger the right side of the implication, the better) and
// in your thesis write why your secrecy lemma implies PFS.
lemma Auto_PFS_Source_Message:
"All #t s_dh_pk j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr m. 
        Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #t
      & (not Ex #x. PQAttack() @ #x)
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x & #x < #t)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x & #x < #t)
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)"

lemma Auto_PQ_PFS_Source_Message:
"All #t s_dh_pk j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr m. 
        Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x & #x < #t)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x & #x < #t)
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)
      | (Ex #x. Reveal_Journalist_EKEM_Key(je_kem_pk) @ #x)"

lemma Auto_Non_Injective_Agreement_Source_Message:
"All #t1 #t2 j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr s_dh_pk m. 
        Journalist_Received(j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, s_dh_pk, m) @ #t1
      & Register_Source(s_dh_pk) @ #t2
  ==>   (Ex #x. Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #x & #x < #t1) 
      | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x)
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)"

// You could simplify this by removing the non-injective part. You showed this
// already. You can look at my PQ3 injectivity lemma. There we only say that
// for all two receive events (with the respective arguments), the timepoint
// must be the same. It follows directly from NI agreement that there also
// exists a respective send event.
lemma Auto_Injective_Agreement_Source_Message:
"All #t1 #t2 j_sig_pk je_dh_pk je_pke_pk je_kem_pk nr s_dh_pk m. 
        Journalist_Received(j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, s_dh_pk, m) @ #t1
      & Register_Source(s_dh_pk) @ #t2
  ==>   (Ex #x1. Source_Sent(s_dh_pk, j_sig_pk, je_dh_pk, je_pke_pk, je_kem_pk, nr, m) @ #x1 
              & #x1 < #t1
              & not (Ex j_sig_pk_2 je_dh_pk_2 je_pke_pk_2 je_kem_pk_2 nr_2 s_dh_pk_2 #x2. Journalist_Received(j_sig_pk_2, je_dh_pk_2, je_pke_pk_2, je_kem_pk_2, nr_2, s_dh_pk_2, m) @ #x2 & not (#x2 = #t1)))
      | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)
      | (Ex #x. Reveal_Journalist_SIG_Key(j_sig_pk) @ #x)
      | (Ex #x. Reveal_Journalist_EDH_Key(je_dh_pk) @ #x)"

// Some of the above comments directly apply to other lemmas too, but I guess
// that's obvious to you :)

lemma Auto_Executability_Journalist_Response:
exists-trace
"Ex #t1 #t2 s_dh_pk j_dh_pk m.
        Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #t1
      & Source_Received(s_dh_pk, j_dh_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

// nicht automatisch verifizierbar
lemma Secrecy_Journalist_Message:
"All #t1 #t2 j_dh_pk s_dh_pk m. 
        Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #t1
      & Register_Source(s_dh_pk) @ #t2
      & (not Ex #x. PQAttack() @ #x)
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)"

// nicht automatisch verifizierbar
lemma PQ_Secrecy_Journalist_Message:
"All #t1 #t2 j_dh_pk s_dh_pk m. 
        Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #t1
      & Register_Source(s_dh_pk) @ #t2
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)"

// nicht automatisch verifizierbar
lemma Non_Injective_Agreement_Journalist_Message:
"All #t1 j_dh_pk s_dh_pk m.
        Source_Received(s_dh_pk, j_dh_pk, m) @ #t1
  ==>   (Ex #x. Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #x & #x < #t1) 
      | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)
      | (Ex #x. Reveal_Journalist_DH_Key(j_dh_pk) @ #x)"
  
// lemma PFS_Journalist_Message:
// "All #t1 #t2 j_dh_pk s_dh_pk m. 
//         Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #t1
//       & Register_Source(s_dh_pk) @ #t2
//   ==>   (not Ex #x. K(m) @ #x)
//       | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x & #x < #t1)"

// lemma Injective_Agreement_Journalist_Message:
// "All #t1 j_dh_pk s_dh_pk m. 
//         Source_Received(s_dh_pk, j_dh_pk, m) @ #t1
//   ==>   (Ex #x1. Journalist_Sent(j_dh_pk, s_dh_pk, m) @ #x1 
//               & #x1 < #t1
//               & not (Ex j_dh_pk_2 s_dh_pk_2 #x2. Source_Received(s_dh_pk_2, j_dh_pk_2, m) @ #x2 & not (#x2 = #t1)))
//       | (Ex #x. Reveal_Source_DH_Key(s_dh_pk) @ #x)
//       | (Ex #x. Reveal_Journalist_DH_Key(j_dh_pk) @ #x)"

end