theory SecureDrop
begin

builtins: symmetric-encryption, asymmetric-encryption, diffie-hellman, signing

// KDF
functions: hkdf/1

/*

Color semantics:

- Yellow (#FDFD96)      Key setup/distribution
- Red (#FF6961)         Adversary activity
- Light blue (#B4D9EF)  Message Fetching
- Dark blue (#6495ED)   Messaging
- Orange (#FF7F50)      Registration/Enrollment

 */

// Key Registration

rule Register_Newsroom_Key [color=#FDFD96]:
  [ Fr(~ltk) ]
  -->
  [ !Ltk_Newsroom($A, ~ltk), !Pk_Newsroom($A, pk(~ltk)), Out(pk(~ltk)) ]

rule Reveal_Newsroom_Key [color=#FF6961]:
  [ !Ltk_Newsroom(A, ltk) ]
  --[ Reveal_Newsroom_Key(A), Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_Source_Encryption_Key [color=#FDFD96]:
  let public_key = pk(~ltk)
  in
  [ Fr(~ltk) ]
  --[ Register_Source($A, public_key) ]->
  [ !Ltk_Source_Encryption($A, ~ltk), !Pk_Source_Encryption($A, public_key) ]

rule Reveal_Source_Encryption_Key [color=#FF6961]:
  let public_key = pk(ltk) 
  in
  [ !Ltk_Source_Encryption(A, ltk) ]
  --[ Reveal_Source_Encryption_Key(A), Reveal_Source_Encryption_Key(public_key), Reveal_Longterm_Secret(A), Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Reveal_Source_Encryption_Public_Key [color=#FF6961]:
  [ !Pk_Source_Encryption(A, public_key) ]
  --[ Reveal_Source_Encryption_Public_Key(A), Reveal_Source_Encryption_Public_Key(public_key), Reveal(A), SomeCompromise() ]->
  [ Out(public_key) ]


// Journalist Enrollment

rule Journalist_Enrollment [color=#FF7F50]:
  let j_pk = pk(~j_sk)
      sig_j_pk = sign(j_pk, nr_sk)
  in
  [ !Ltk_Newsroom($NR, nr_sk)
  , Fr(~j_sk)]
  --[ Journalist_Verified($NR, $J)
    , OnlyOnce($J) ]->
  [ Out(<j_pk, sig_j_pk>)
  , !Journalist_Enrolled($NR, $J)
  , !Ltk_Journalist_Signing($J, ~j_sk)
  , !Pk_Journalist_Signing($J, j_pk) ]

rule Reveal_Journalist_Signing_Key [color=#FF6961]:
  [ !Ltk_Journalist_Signing(A, ltk) ]
  --[ Reveal_Journalist_Signing_Key(A), Reveal_Longterm_Secret(A), Reveal(A), SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Register_Ephemeral_Key [color=#FF7F50]:
  let je_pk = pk(~je_sk)
      sig_je_pk = sign(<'ephemeral', je_pk>, j_sk)
  in
  [ !Ltk_Journalist_Signing($J, j_sk)
  , Fr(~je_sk) ]
  -->
  [ Out(<je_pk, sig_je_pk>)
  , Journalist_Ephemeral_Key($J, ~je_sk)
  , !Journalist_Ephemeral_Public_Key($J, pk(~je_sk))
  , Journalist_Ephemeral_Key_Reveal($J, ~je_sk) ]

rule Reveal_Journalist_Ephemeral_Key [color=#FF6961]:
  let public_key = pk(je_sk)
  in
  [ Journalist_Ephemeral_Key_Reveal(J, je_sk) ]
  --[ Reveal_Journalist_Ephemeral_Key(J, public_key), Reveal(J), SomeCompromise() ]->
  [ Out(je_sk) ]

// Messaging and Fetching Protocol

rule Source_Submit [derivchecks, color=#6495ED]:
  let s_pk = pk(s_sk)
      k = hkdf(~ss)
      m = <'msg', ~msg, s_pk>
      c = senc(m, k)
      ss_encap = aenc(~ss, je_pk)
  in
  [ !Ltk_Source_Encryption($S, s_sk)
  , !Pk_Newsroom($NR, nr_pk)
  , !Pk_Journalist_Signing($J, j_pk)
  , In(<j_pk, sig_j_pk, je_pk, sig_je_pk>)
  , Fr(~msg)
  , Fr(~ss) ]
  --[ // check signatures of journalist's longterm signature and ephemeral encryption keys
      Eq(verify(sig_j_pk, j_pk, nr_pk), true)
    , Eq(verify(sig_je_pk, <'ephemeral', je_pk>, j_pk), true)
      // Actions for Lemmas
    , Symmetric_Encryption_OUT(m, k)
    , Source_Sent(s_pk, $J, je_pk, $NR, m)
    ]->
  [ Out(<c, ss_encap>) ]

rule Journalist_Recv [derivchecks, color=#6495ED]:
  let ss = adec(ss_encap, je_sk)
      k = hkdf(ss)
      m = <'msg', msg, s_pk>
      je_pk = pk(je_sk)
  in
  [ In(<senc(m, k), ss_encap>)
  , Journalist_Ephemeral_Key($J, je_sk)
  , !Journalist_Enrolled($NR, $J)]
  --[ // Actions for Lemmas
      Symmetric_Encryption_IN(m, k)
    , Journalist_Received($J, je_pk, $NR, s_pk, m)
    ]->
  [ Journalist_Recv($J, $NR, s_pk, msg) ]

rule Journalist_Reply [color=#6495ED]:
  let k = hkdf(~ss)
      je_pk = pk(je_sk)
      m = <'msg', ~msg, $J>
      c = senc(m, k)
      ss_encap = aenc(~ss, s_pk)
  in
  [ Journalist_Recv($J, $NR, s_pk, msg_old)
  , Journalist_Ephemeral_Key($J, je_sk)
  , Fr(~msg)
  , Fr(~ss) ]
  --[ // Actions for Lemmas
      Symmetric_Encryption_OUT(m, k)
    , Journalist_Sent($J, $NR, s_pk, m)
    ]->
  [ Out(<c, ss_encap>) ]

rule Source_Recv [color=#6495ED]:
  let ss = adec(ss_encap, s_sk)
      k = hkdf(ss)
      m = <'msg', msg, $J>
      s_pk = pk(s_sk)
  in
  [ In(<senc(m, k), ss_encap>)
  , !Ltk_Source_Encryption($S, s_sk) ]
  --[ // Actions for Lemmas
      Symmetric_Encryption_IN(m, k)
    , Source_Recieved(s_pk, m)
    ]->
  [ Source_Recv($S, msg, $J) ]

// rule Source_Reply [derivchecks, color=#6495ED]:
//   let s_pk = 'g'^s_sk
//       k = je_pk^~me_sk
//       m = <'msg', ~msg, s_pk>
//       c = senc(m, k)
//       me_pk = 'g'^~me_sk
//   in
//   [ Source_Recv($S, msg_old, $J)
//   , !Ltk_Source_Encryption($S, s_sk)
//   , !Pk_Newsroom($NR, nr_pk)
//   , !Pk_Journalist_Signing($J, j_pk)
//   , In(<j_pk, sig_j_pk, je_pk, sig_je_pk>)
//   , Fr(~msg)
//   , Fr(~ss)
//   , Fr(~me_sk) ]
//   --[ Eq(verify(sig_j_pk, j_pk, nr_pk), true)
//     , Eq(verify(sig_je_pk, <'ephemeral', je_pk>, j_pk), true)
//     , Symmetric_Encryption_OUT(m, k)
//     , Source_Message_Sent($S, $J, je_pk, $NR, ~msg)
//     , Source_Response($S, $J, $NR, msg_old, ~msg)
//     , Sent($S, $J, m) ]->
//   [ Out(<c, me_pk>) ]

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

// restriction DHElement_Check:
//   "All x #t. DHElement_Check(x) @ #t ==> not(x = 'g' | x = DH_neutral)"

restriction OnlyOnce:
  "All x #i #j. OnlyOnce(x)@#i & OnlyOnce(x)@#j ==> #i = #j"

/* Without this Source-Lemma, Tamarin thinks that the "Fetch" Rules act as decryption oracles.
   This Source-Lemma ensures that if a party gets a ciphertext, either the adversary encrypted the message (i.e, it knew the message)
   or the adversary got the ciphertext from an output in the protocol.
*/
lemma Symmetric_Encryption [sources]:
"All m key #t.
        Symmetric_Encryption_IN(m, key) @ #t
  ==>   ( (Ex #x. KU(m) @ #x & #x < #t)
        & (Ex #x. KU(key) @ #x & #x < #t))
      | (Ex #x. Symmetric_Encryption_OUT(m, key) @ #x & #x < #t)"

lemma Executability_Submission:
exists-trace
"Ex #t1 #t2 s_pk j je_pk nr m.
        Source_Sent(s_pk, j, je_pk, nr, m) @ #t1
      & Journalist_Received(j, je_pk, nr, s_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Executability_Journalist_Response:
exists-trace
"Ex #t1 #t2 s_pk j nr m.
        Journalist_Sent(j, nr, s_pk, m) @ #t1
      & Source_Recieved(s_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

// lemma Executability_Source_Response:
// exists-trace
// "Ex #t1 #t2 #t3 #t4 #t5 #t6 s j nr m_old m m_new.
//         Source_Submission(s, j, nr, m_old) @ #t1
//       & Journalist_Read(j, m_old) @ #t2
//       & Journalist_Response(j, m_old, m) @ #t3
//       & Source_Read(s, m) @ #t4
//       & Source_Response(s, j, nr, m, m_new) @ #t5
//       & Journalist_Read(j, m_new) @ #t6
//       & (not Ex #x. SomeCompromise() @ #x)"

lemma Secrecy_Source_Message:
"All #t s j je_pk nr m. 
        Source_Sent(s, j, je_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j) @ #x)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(j, je_pk) @ #x)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x)"

lemma PFS_Source_Message:
"All #t s j je_pk nr m. 
        Source_Sent(s, j, je_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j) @ #x & #x < #t)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(j, je_pk) @ #x)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x & #x < #t)"

lemma Secrecy_Journalist_Message:
"All #t1 #t2 j nr s s_pk m. 
        Journalist_Sent(j, nr, s_pk, m) @ #t1
      & Register_Source(s, s_pk) @ #t2
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_Encryption_Key(s) @ #x)"

lemma PFS_Journalist_Message:
"All #t j nr s m. 
        Journalist_Sent(j, nr, s, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_Encryption_Key(s) @ #x & #x < #t)"

lemma Non_Injective_Agreement_Source_Message:
"All #t j je_pk_1 nr s_pk m. 
        Journalist_Received(j, je_pk_1, nr, s_pk, m) @ #t
  ==>   (Ex #x je_pk_2. Source_Sent(s_pk, j, je_pk_2, nr, m) @ #x & #x < #t) 
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal(j) @ #x)
      | (Ex #x. Reveal(nr) @ #x)"
  
lemma Injective_Agreement_Source_Message:
"All #t j je_pk_1 nr s_pk m. 
        Journalist_Received(j, je_pk_1, nr, s_pk, m) @ #t
  ==>   (Ex #x je_pk_2. Source_Sent(s_pk, j, je_pk_2, nr, m) @ #x 
              & #x < #t
              & not (Ex j_2 je_pk_3 nr_2 s_pk_2 #t2. Journalist_Received(j_2, je_pk_3, nr_2, s_pk_2, m) @ #t2 & not (#t2 = #t)))
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal(j) @ #x)
      | (Ex #x. Reveal(nr) @ #x)"

end