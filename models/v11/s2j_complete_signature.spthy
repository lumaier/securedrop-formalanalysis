theory SecureDrop
begin

builtins: asymmetric-encryption, signing

// KDF
functions: hkdf/1 

/*

Color semantics:

- Yellow (#FDFD96)      Key setup/distribution
- Red (#FF6961)         Adversary activity
- Light blue (#B4D9EF)  Message Fetching
- Dark blue (#6495ED)   Messaging
- Orange (#FF7F50)      Registration/Enrollment

 */

// Key Registration

rule Register_Newsroom_Key [color=#FDFD96]:
  let public_key = pk(~ltk) in
  [ Fr(~ltk) ]
  -->
  [ !Ltk_Newsroom($A, ~ltk)
  , !Pk_Newsroom($A, public_key)
  , Out(public_key) ]

rule Reveal_Newsroom_Key [color=#FF6961]:
  [ !Ltk_Newsroom(A, ltk) ]
  --[ Reveal_Newsroom_Key(A)
    , Reveal(A)
    , SomeCompromise() ]->
  [ Out(ltk) ]

rule Register_Source_Encryption_Key [color=#FDFD96]:
  let public_key = pk(~ltk) in
  [ Fr(~ltk) ]
  --[ Register_Source(public_key) ]->
  [ !Ltk_Source_Encryption($A, ~ltk)
  , !Pk_Source_Encryption($A, public_key)
  , Out(public_key) ]

rule Reveal_Source_Encryption_Key [color=#FF6961]:
  let public_key = pk(ltk) in
  [ !Ltk_Source_Encryption(A, ltk) ]
  --[ Reveal_Source_Encryption_Key(A)
    , Reveal_Source_Encryption_Key(public_key)
    , Reveal_Longterm_Secret(A)
    , Reveal(A)
    , SomeCompromise() ]->
  [ Out(ltk) ]


// Journalist Enrollment

rule Journalist_Enrollment [color=#FF7F50]:
  let j_pk = pk(~j_sk)
      sig_j_pk = sign(<'signing', j_pk>, nr_sk)
  in
  [ !Ltk_Newsroom($NR, nr_sk)
  , Fr(~j_sk)]
  --[ Journalist_Verified($NR, $J)
    , OnlyOnce($J) ]->
  [ Out(<j_pk, sig_j_pk>)
  , !Journalist_Enrolled($NR, $J)
  , !Ltk_Journalist_Signing($J, ~j_sk)
  , !Pk_Journalist_Signing($J, j_pk) ]

rule Reveal_Journalist_Signing_Key [color=#FF6961]:
  let public_key = pk(ltk) in
  [ !Ltk_Journalist_Signing(A, ltk) ]
  --[ Reveal_Journalist_Signing_Key(A)
    , Reveal_Journalist_Signing_Key(public_key)
    , Reveal_Longterm_Secret(A)
    , Reveal(A)
    , SomeCompromise() ]->
  [ Out(ltk) ]

rule Journalist_Register_Ephemeral_Key [color=#FF7F50]:
  let je_pk = pk(~je_sk)
      sig_je_pk = sign(<'ephemeral', je_pk>, j_sk)
  in
  [ !Ltk_Journalist_Signing($J, j_sk)
  , Fr(~je_sk) ]
  -->
  [ Out(<je_pk, sig_je_pk>)
  , !Journalist_Ephemeral_Key($J, ~je_sk)
  , Journalist_Ephemeral_Key_Reveal($J, ~je_sk) ]

rule Reveal_Journalist_Ephemeral_Key [color=#FF6961]:
  let public_key = pk(je_sk) in
  [ Journalist_Ephemeral_Key_Reveal(J, je_sk) ]
  --[ Reveal_Journalist_Ephemeral_Key(public_key)
    , Reveal(J)
    , SomeCompromise() ]->
  [ Out(je_sk) ]

// Messaging and Fetching Protocol

// rule Source_Submission [color=#6495ED]:
//   [ In(je_pk)
//   , Fr(~msg) ]
//   -->
//   [ Source_Recv($S, ~msg, $J, je_pk) ]

rule Source_Send [color=#6495ED]:
  let s_pk = pk(s_sk)
      m = <'msg_s2j', ~msg, s_pk, je_pk, $J, $NR, sign(<'msg_s2j', ~msg, s_pk, je_pk, $J, $NR>, s_sk)>
      c = aenc(m, je_pk)
  in
  [ // Source_Recv($S, ~msg, $J, je_pk)
    !Ltk_Source_Encryption($S, s_sk)
  , !Pk_Newsroom($NR, nr_pk)
  , !Pk_Journalist_Signing($J, j_pk)
  , In(<j_pk, sig_j_pk, je_pk, sig_je_pk>)
  , Fr(~msg) ]
  --[ // check signatures of journalist's longterm signature and ephemeral encryption keys
      Eq(verify(sig_j_pk, <'signing', j_pk>, nr_pk), true)
    , Eq(verify(sig_je_pk, <'ephemeral', je_pk>, j_pk), true)
      // Actions for Lemmas
    , Source_Sent(s_pk, $J, je_pk, $NR, m)
    ]->
  [ Out(c) ]

rule Journalist_Recv [color=#6495ED]:
  let je_pk = pk(je_sk)
      m = <'msg_s2j', msg, s_pk, je_pk, $J, $NR, sig_m>
  in
  [ In(aenc(m, pk(je_sk)))
  , !Journalist_Ephemeral_Key($J, je_sk)
  , !Journalist_Enrolled($NR, $J)]
  --[ OnlyOnce(<$J, je_sk>)
    , Eq(verify(sig_m, <'msg_s2j', msg, s_pk, je_pk, $J, $NR>, s_pk), true)
      // Actions for Lemmas
    , Journalist_Received($J, je_pk, $NR, s_pk, m)
    ]->
  [ Journalist_Recv($J, $NR, s_pk, msg) ]

rule Journalist_Send [color=#6495ED]:
  let j_pk = pk(j_sk)
      m = <'msg_j2s', ~msg, j_pk, s_pk, sign(<'msg_j2s', ~msg, j_pk, s_pk>, j_sk)>
      c = aenc(m, s_pk)
  in
  [ Journalist_Recv($J, $NR, s_pk, msg_old)
  , !Ltk_Journalist_Signing($J, j_sk)
  , Fr(~msg) ]
  --[ // Actions for Lemmas
      Journalist_Sent($J, s_pk, m)
    ]->
  [ Out(c) ]

rule Source_Recv [color=#6495ED]:
  let s_pk = pk(s_sk)
      m = <'msg_j2s', msg, j_pk, s_pk, sig_m>
  in
  [ In(aenc(m, pk(s_sk)))
  , !Ltk_Source_Encryption($S, s_sk)
  , !Pk_Journalist_Signing($J, j_pk) ]
  --[ // Actions for Lemmas
      Eq(verify(sig_m, <'msg_j2s', msg, j_pk, s_pk>, j_pk), true)
    , Source_Received(s_pk, $J, m)
    ]->
  [ Source_Recv($S, msg, j_pk) ]

// rule Source_Reply [color=#6495ED]:
//   let s_pk = 'g'^s_sk
//       k = (je_pk^~me_sk)^s_sk
//       m = <'msg', ~msg, ~me_sk, s_pk, $J, $NR>
//       c = senc(m, k)
//       me_pk = ('g'^~me_sk)^s_sk
//   in
//   [ Source_Recv($S, msg, $J, je_pk)
//   , !Ltk_Source_Encryption($S, s_sk)
//   , !Pk_Newsroom($NR, nr_pk)
//   , !Pk_Journalist_Signing($J, j_pk)
//   , In(<j_pk, sig_j_pk, je_pk, sig_je_pk>)
//   , Fr(~msg)
//   , Fr(~ss)
//   , Fr(~me_sk) ]
//   --[ // check signatures of journalist's longterm signature and ephemeral encryption keys
//       Eq(verify(sig_j_pk, j_pk, nr_pk), true)
//     , Eq(verify(sig_je_pk, <'ephemeral', je_pk>, j_pk), true)
//       // sanity check of DH elements used
//     , DHElement_Check(je_pk)
//     , DHElement_Check(k)
//     , DHElement_Check(me_pk)
//       // Actions for Lemmas
//     , Symmetric_Encryption_OUT(m, k)
//     , Source_Sent(s_pk, $J, je_pk, $NR, m)
//     ]->
//   [ Out(<c, me_pk>) ]

restriction Eq:
  "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

restriction OnlyOnce:
  "All x #i #j. OnlyOnce(x)@#i & OnlyOnce(x)@#j ==> #i = #j"

lemma Auto_Executability_Submission:
exists-trace
"Ex #t1 #t2 s_pk j je_pk nr m.
        Source_Sent(s_pk, j, je_pk, nr, m) @ #t1
      & Journalist_Received(j, je_pk, nr, s_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Auto_Executability_Journalist_Response:
exists-trace
"Ex #t1 #t2 s_pk j_pk m.
        Journalist_Sent(j_pk, s_pk, m) @ #t1
      & Source_Received(s_pk, j_pk, m) @ #t2
      & (not Ex #x. SomeCompromise() @ #x)"

lemma Auto_Secrecy_Source_Message:
"All #t s j je_pk nr m. 
        Source_Sent(s, j, je_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j) @ #x)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(je_pk) @ #x)"

lemma Auto_PFS_Source_Message:
"All #t s j je_pk nr m. 
        Source_Sent(s, j, je_pk, nr, m) @ #t
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j) @ #x & #x < #t)
      | (Ex #x. Reveal_Newsroom_Key(nr) @ #x & #x < #t)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(je_pk) @ #x)"

lemma Auto_Secrecy_Journalist_Message:
"All #t1 #t2 j_pk s_pk m. 
        Journalist_Sent(j_pk, s_pk, m) @ #t1
      & Register_Source(s_pk) @ #t2
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)"

lemma Auto_PFS_Journalist_Message:
"All #t1 #t2 j_pk s_pk m. 
        Journalist_Sent(j_pk, s_pk, m) @ #t1
      & Register_Source(s_pk) @ #t2
  ==>   (not Ex #x. K(m) @ #x)
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x & #x < #t1)"

lemma Auto_Non_Injective_Agreement_Source_Message:
"All #t1 #t2 j je_pk nr s_pk m. 
        Journalist_Received(j, je_pk, nr, s_pk, m) @ #t1
      & Register_Source(s_pk) @ #t2
  ==>   (Ex #x. Source_Sent(s_pk, j, je_pk, nr, m) @ #x & #x < #t1) 
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(je_pk) @ #x)"
  
lemma Auto_Injective_Agreement_Source_Message:
"All #t1 #t2 j je_pk nr s_pk m. 
        Journalist_Received(j, je_pk, nr, s_pk, m) @ #t1
      & Register_Source(s_pk) @ #t2
  ==>   (Ex #x1. Source_Sent(s_pk, j, je_pk, nr, m) @ #x1 
              & #x1 < #t1
              & not (Ex j_2 je_pk_2 nr_2 s_pk_2 #x2. Journalist_Received(j_2, je_pk_2, nr_2, s_pk_2, m) @ #x2 & not (#x2 = #t1)))
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal_Journalist_Ephemeral_Key(je_pk) @ #x)"

lemma Auto_Non_Injective_Agreement_Journalist_Message:
"All #t1 j_pk s_pk m. 
        Source_Received(s_pk, j_pk, m) @ #t1
  ==>   (Ex #x. Journalist_Sent(j_pk, s_pk, m) @ #x & #x < #t1) 
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j_pk) @ #x)"
  
lemma Auto_Injective_Agreement_Journalist_Message:
"All #t1 j_pk s_pk m. 
        Source_Received(s_pk, j_pk, m) @ #t1
  ==>   (Ex #x1. Journalist_Sent(j_pk, s_pk, m) @ #x1 
              & #x1 < #t1
              & not (Ex j_pk_2 s_pk_2 #x2. Source_Received(s_pk_2, j_pk_2, m) @ #x2 & not (#x2 = #t1)))
      | (Ex #x. Reveal_Source_Encryption_Key(s_pk) @ #x)
      | (Ex #x. Reveal_Journalist_Signing_Key(j_pk) @ #x)"

end